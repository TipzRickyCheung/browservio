import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def sdkDir = properties.getProperty('git.exec')
            commandLine 'cmd', '/c', sdkDir, 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        } else {
            commandLine 'git', 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        }
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace "tipz.viola"
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 34

    defaultConfig {
        applicationId "tipz.viola"
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 1
        versionName "6.0.0000.00"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Viola\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("E MMM dd HH:mm:ss z yyyy") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        resConfigs 'de-rDE', 'en-rGB', 'fr-rFR', 'tr', 'vi', 'zh-rCN', 'zh-rTW'

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        debug {
            res.srcDir "debug/res"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0-alpha02'
    implementation 'com.loopj.android:android-async-http:1.4.11'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.webkit:webkit:1.7.0-alpha03'
    implementation('androidx.preference:preference:1.2.0') {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:2.5.0"
    annotationProcessor 'androidx.annotation:annotation:1.6.0'
    annotationProcessor "androidx.room:room-compiler:2.5.0"
}
