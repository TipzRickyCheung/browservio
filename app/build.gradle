plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
}

def getGitRevision = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    namespace "tipz.viola"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "tipz.viola"
        compileSdk 34
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 51
        versionName "7.0"

        /* Extra build info */
        buildConfigField "String", "PRODUCT_LICENSE_DOCUMENT", "\"Apache License, Version 2.0\""
        buildConfigField "String", "VERSION_CODENAME", "\"Exotic\""
        buildConfigField "String", "VERSION_COPYRIGHT_YEAR", "\"2020-2024\""
        buildConfigField "String", "VERSION_BUILD_ID", "\"${versionCode}\""
        buildConfigField "String", "VERSION_BUILD_REVISION", "\"0\""
        buildConfigField "String", "VERSION_BUILD_GIT_REVISION", "\"${getGitRevision()}\""
        buildConfigField "String", "VERSION_BUILD_BRANCH", "\"${getGitBranch()}\""
        buildConfigField "String", "VERSION_BUILD_DATE_MINIMAL", "\"" + new Date().format("yyMMdd") + "\""
        buildConfigField "String", "VERSION_BUILD_TIME_MINIMAL", "\"" + new Date().format("HHmm") + "\""

        /* Legacy functions */
        buildConfigField "String", "VERSION_BUILD_DATE_FULL", "\"" + new Date().format("E MMM dd HH:mm:ss z yyyy") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        resConfigs 'de-rDE', 'en-rGB', 'fr-rFR', 'tr', 'vi', 'zh-rCN', 'zh-rTW'

    }
    buildFeatures {
        buildConfig = true
    }
    buildTypes {
        // Generic release
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        // Stable release
        release_stable {
            initWith buildTypes.release
        }

        // Next release
        release_next {
            applicationIdSuffix ".next"
            initWith buildTypes.release
        }

        // Debug internal
        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            buildConfigField "String", "VERSION_BUILD_ID", "null"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError = false
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        // Next release
        release_next {
            res.srcDir "next/res"
        }

        // Debug internal
        debug {
            res.srcDir "debug/res"
        }
    }
}

dependencies {
    def room_version = "2.6.1"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.webkit:webkit:1.11.0'
    implementation('androidx.preference:preference-ktx:1.2.1') {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    annotationProcessor 'androidx.annotation:annotation:1.8.0'
    annotationProcessor "androidx.room:room-compiler:$room_version"
}
