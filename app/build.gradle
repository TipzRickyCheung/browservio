apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 31

    defaultConfig {
        applicationId "tipz.browservio"
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 57
        versionName "2.0.1"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Change\""
        buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_change_patch1\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("yyMMdd-HHmm") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""
        buildConfigField "boolean", "UPDATE_TESTING", "false"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }

        debug {
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
}
