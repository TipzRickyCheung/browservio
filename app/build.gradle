plugins {
    id 'com.android.application'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--match=NeVeRmAtCh', '--always', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 33

    defaultConfig {
        applicationId "tipz.browservio"
        minSdkVersion 14
        targetSdkVersion 33
        versionCode 69420
        versionName "MASTER"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Dragonheart\""
        buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_dragonheart_master\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("yyMMdd-HHmm") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        resConfigs "de-rDE", "en-rGB", "tr", "vi", "zh-rCN", "zh-rTW"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }

        snapshot {
            initWith(buildTypes.release)
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.7.0-alpha03'
    implementation 'androidx.webkit:webkit:1.5.0-beta01'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation "androidx.room:room-runtime:2.4.2"
    annotationProcessor "androidx.room:room-compiler:2.4.2"
}
