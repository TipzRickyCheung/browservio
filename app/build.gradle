apply plugin: 'com.android.application'
apply plugin: 'idea'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 32

    defaultConfig {
        applicationId "tipz.browservio"
        minSdkVersion 14
        targetSdkVersion 32
        versionCode 59
        versionName "2.1"

        buildConfigField "String", "VERSION_NAME_EXTRA", "\"\""
        buildConfigField "String", "VERSION_CODENAME", "\"Cyanogen\""
        buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_cyanogen_rel\""
        buildConfigField "String", "VERSION_BUILD_DATE", "\"" + new Date().format("yyMMdd-HHmm") + "\""
        buildConfigField "String", "VERSION_BUILD_YEAR", "\"" + new Date().format("yyyy") + "\""

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }

        debug {
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }

        snapshot {
            initWith(buildTypes.release)
            buildConfigField "String", "VERSION_NAME_EXTRA", "\" ${getGitHash()}\""
            buildConfigField "String", "VERSION_TECHNICAL_EXTRA", "\"_${getGitHash()}\""
        }
    }
}

idea {
    module {
        excludeDirs = [file("src/main/res/raw/hosts.txt")]
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0-alpha03'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
}
